
plugins {
    id 'java-library'
    id 'jacoco'
    id 'jacoco-report-aggregation'
    id 'signing'
    id 'maven-publish'
    id 'org.sonarqube' version '7.0.1.6134'
    id "io.github.gradle-nexus.publish-plugin" version "2.0.0"
    id 'com.github.ben-manes.versions' version '0.53.0'
}

group = 'org.itsallcode'
version = '0.3.0'

repositories {
    mavenCentral()
}

dependencies {
}

testing {
    suites {
        test {
            useJUnitJupiter(libs.versions.junitJupiter.get())
            dependencies {
                implementation libs.assertj
                implementation libs.mockitoJunit
            }
            targets.all {
                testTask.configure {
                    systemProperty 'java.util.logging.config.file', file('src/test/resources/logging.properties')
                }
            }
        }
    }
}

java {
    toolchain {
        def javaVersion = project.hasProperty('javaVersion') ? project.getProperty('javaVersion') : 25
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    failOnError = true
    options.addStringOption('Xwerror', '-quiet')
    options.addBooleanOption('linksource', true)
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all'
    options.compilerArgs << '-Werror'
    options.encoding = 'UTF-8'
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

test {
    finalizedBy jacocoTestReport
}

sonar {
  properties {
    property("sonar.organization", "itsallcode")
    property("sonar.host.url", "https://sonarcloud.io")
  }
}

rootProject.tasks['sonar'].dependsOn(tasks['testCodeCoverageReport'])

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Simple Process API'
                description = 'Library to simplify running processes'
                url = 'https://github.com/itsallcode/simple-process'

                licenses {
                    license {
                        name = 'GNU General Public License, Version 3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kaklakariada'
                        name = 'Christoph'
                        email = 'kaklakariada@chp1.net'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/itsallcode/simple-process.git'
                    developerConnection = 'scm:git:https://github.com/itsallcode/simple-process.git'
                    url = 'https://github.com/itsallcode/simple-process'
                }
            }
        }
    }
}

signing {
    required = { gradle.taskGraph.hasTask("publish") }
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

nexusPublishing {
    packageGroup = project.group
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    gradleReleaseChannel = "current"
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}
